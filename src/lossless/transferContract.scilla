scilla_version 0

import BoolUtils IntUtils PairUtils ListUtils

library LosslessLotteryDepositTransfer 

type Error =
| NotOwner
| PendingWithdrawal
| NotStakingContract
| IncorrectPercentage
| IncorrectSsn
| NotRecompoundingContract

let make_error =
fun (result : Error) =>
  let result_code = 
    match result with
    | NotOwner             => Int32 -1
    | PendingWithdrawal                 => Int32 -2
    | NotStakingContract => Int32 -3
    | IncorrectPercentage => Int32 -4
    | IncorrectSsn => Int32 -5
    | NotRecompoundingContract => Int32 -6
    end
  in
  { _exception : "ContractError"; code : result_code }

let option_value =
    tfun 'A =>
        fun (default: 'A) =>
        fun (opt_val: Option 'A) =>
        match opt_val with
            | Some v => v
            | None => default
        end

let option_map_bystr20_uint128_value =
    let f = @option_value (Map ByStr20 Uint128) in
        let emt = Emp ByStr20 Uint128 in
        f emt

let one_msg = fun(msg: Message) =>
    let nil_msg = Nil {Message} in
    Cons {Message} msg nil_msg

let two_msgs =
    fun (msg1 : Message) =>
    fun (msg2 : Message) =>
        let msgs_tmp = one_msg msg2 in
        Cons {Message} msg1 msgs_tmp

let zero: Uint128 = Uint128 0
let hundred: Uint128= Uint128 100

contract LosslessLotteryDepositTransfer(main_contract: ByStr20, owner: ByStr20, staking_contract_init: ByStr20,
ssn: ByStr20, staking_contract_proxy: ByStr20)

field recompounding_contract: ByStr20 = main_contract
field staking_contract: ByStr20 = staking_contract_init
field gzil_contract: ByStr20 = 0xf3ff53bacc824678b2833ea0edb82b01e8f570ed
field backers_deposit: Map ByStr20 Uint128 = Emp ByStr20 Uint128
field backers_p_lottery: Map ByStr20 Uint128 = Emp ByStr20 Uint128
field backers_p_interest: Map ByStr20 Uint128 = Emp ByStr20 Uint128
field user_withdrawal_dict: Option Map BNum Uint128 = None {Map BNum Uint128}
field current_yield: Uint128 = Uint128 0
field amount_deposited: Uint128 = Uint128 0
field reward_dict: Map ByStr20 Uint128 = Emp ByStr20 Uint128
field ssn_contract: ByStr20 = ssn
field user_deleg_dict_curr: Map ByStr20 Uint128 = Emp ByStr20 Uint128

procedure ThrowError(err : Error)
  e = make_error err;
  throw e
end

procedure IsOwner(initiator: ByStr20)
  is_owner = builtin eq initiator owner;
  match is_owner with
    | True =>
    | False =>
        err = NotOwner;
        ThrowError err
    end
end

procedure transferGzilToOwner(amount: Uint128)
  gzil_contract_local <- gzil_contract; 
  msg = {
    _tag: "Transfer";
    _recipient: _sender;
    _amount: zero;
    to: gzil_contract_local;
    amount: amount
  };
  msgs = one_msg msg;
  send msgs
end

transition ownerBalanceWithdraw()
    IsOwner _sender;
    balance <- _balance;
    msg = {
        _tag: "AddFunds";
        _amount: balance;
        _recipient: owner
    };
    msgs = one_msg msg;
    send msgs
end

procedure ownerBalanceWithdraw_pd()
    IsOwner _sender;
    balance <- _balance;
    msg = {
        _tag: "AddFunds";
        _amount: balance;
        _recipient: owner
    };
    msgs = one_msg msg;
    send msgs
end

transition ownershipTransferToRecompoundingContract()
    recompounding_contract_local <- recompounding_contract;
    staking_contract_local <- staking_contract;
    msg = {
        _tag: "RequestDelegatorSwap";
        _amount: zero;
        _recipient: staking_contract_proxy;
        new_deleg_addr: recompounding_contract_local
    };
    msgs = one_msg msg;
    send msgs
end

transition updateMainContract(new_contract: ByStr20)
  IsOwner _sender;
  recompounding_contract := new_contract
end

procedure checkValidPercentage(interest_percent: Uint128)
(* statements go below *)
    is_ge_than_zero = uint128_ge interest_percent zero;
    is_le_than_hundred = uint128_le interest_percent hundred;
    match is_ge_than_zero with
    |   True =>
        match is_le_than_hundred with
        |   True =>
        |   False =>
            err = IncorrectPercentage;
            ThrowError err
        end
    |   False =>
        err = IncorrectPercentage;
        ThrowError err
    end
end

procedure isStakingContract(staking_contract_local: ByStr20)
    staking_contract_curr <- staking_contract;
    is_staking_contract = builtin eq staking_contract_curr staking_contract_local;
    match is_staking_contract with
    |   True =>
    |   False =>
        err = NotStakingContract;
        ThrowError err
    end
end

procedure isRecompoundingContract(recompounding_contract_local: ByStr20)
    recompounding_contract_curr <- recompounding_contract;
    is_recompounding_contract = builtin eq recompounding_contract_curr recompounding_contract_local;
    match is_recompounding_contract with
    |   True =>
    |   False =>
        err = NotRecompoundingContract;
        ThrowError err
    end
end


procedure userHasNoWithdrawal()
    withdrawal_dict <- user_withdrawal_dict;
    match withdrawal_dict with
    | None =>
    | Some withdrawal_dict =>
        err = PendingWithdrawal;
        ThrowError err
    end
end

procedure updateUserDeposit(userDeposit: Option Uint128)
    current_ssn_local <- ssn_contract;
    match userDeposit with
    | None =>
    | Some amt =>
        backers_deposit[_sender]:=amt;
        amount_deposited_local <- amount_deposited;
        amount_deposited_local = builtin add amount_deposited_local amt;
        amount_deposited := amount_deposited_local
    end
end

procedure updatePercentages(compound_percent: Uint128)
    lottery_percent = builtin sub hundred compound_percent;
    backers_p_lottery[_sender] := lottery_percent;
    backers_p_interest[_sender] := compound_percent
end

procedure checkValidSsn(user_ssn_deposit: Pair ByStr20 Uint128)
    ssn_contract_local <- ssn_contract;
    match user_ssn_deposit with
    |   Pair user_ssn deposit =>
          is_correct = builtin eq user_ssn ssn_contract_local;
          match is_correct with
          |   True =>
          |   False =>
              err = IncorrectSsn;
              ThrowError err
          end
    end
end

procedure checkUserSsn()
    ssn_amount <- user_deleg_dict_curr;
    ssn_amount_deposit = builtin to_list ssn_amount;
    forall ssn_amount_deposit checkValidSsn
end

transition confirmUserTransferToContract(staking_contract_local : ByStr20 with contract
    field withdrawal_pending: Map ByStr20 (Map BNum Uint128),
    field deposit_amt_deleg: Map ByStr20 (Map ByStr20 Uint128)
    end, recompounding_percent: Uint128)

    isStakingContract staking_contract_local;
    checkValidPercentage recompounding_percent;

    withdrawal_dict <- & staking_contract_local.withdrawal_pending[_sender];
    user_withdrawal_dict := withdrawal_dict;
    user_deleg_dict <- & staking_contract_local.deposit_amt_deleg[_sender];
    user_deleg_dict = option_map_bystr20_uint128_value user_deleg_dict;
    user_deleg_dict_curr := user_deleg_dict;
    

    userHasNoWithdrawal;
    checkUserSsn;
    
    current_ssn_local <- ssn_contract;
    userDeposit <- & staking_contract_local.deposit_amt_deleg[_sender][current_ssn_local];
    
    updateUserDeposit userDeposit;
    updatePercentages recompounding_percent;

    msg = {
        _tag: "ConfirmDelegatorSwap";
        _amount: zero;
        _recipient: staking_contract_proxy;
        requestor: _sender
    };
    msgs = one_msg msg;
    send msgs
end


procedure updateBackerReward(backer: ByStr20, amount: Uint128)
(* statements go below *)
    current_reward <- reward_dict[backer];
    match current_reward with
    | Some amt =>
        new_reward = builtin add amount amt;
        reward_dict[backer] := new_reward
    | None =>
        reward_dict[backer] := amount
    end
end

procedure updateYield(backer_amount: Pair ByStr20 Uint128)
(* statements go below *)
    current_yield_local <- current_yield;
    total_deposit_local <- amount_deposited;
    match backer_amount with
    | Pair backer amount =>
        backer_yield = builtin mul current_yield_local amount;
        backer_yield = builtin div backer_yield total_deposit_local;
        updateBackerReward backer backer_yield     
    end
end


transition AddFunds()
    isStakingContract _sender;
    accept;
    current_yield := _amount;
    backers_deposit_local <- backers_deposit;
    backers_amounts = builtin to_list backers_deposit_local;
    forall backers_amounts updateYield
end

transition WithdrawStakeRewardsSuccessCallBack(ssnaddr: ByStr20, rewards: Uint128)
end

transition fetch_yield()
    IsOwner _sender;
    staking_contract_local <- staking_contract;
    ssn_addr_local <- ssn_contract;
    msg = {
        _recipient: staking_contract_proxy;
        _amount: zero;
        _tag: "WithdrawStakeRewards";
        ssn_addr: ssn_addr_local
    };
    msgs = one_msg msg;
    send msgs
end

procedure withdrawReward(amt: Uint128)
(* statements go below *)
    msg = {
        _recipient : _sender;
        _amount: amt;
        _tag: "AddFunds"
    };
    msgs = one_msg msg;
    reward_dict[_sender] := zero;
    send msgs
end

transition withdrawUserReward()
   amount <- reward_dict[_sender];
   match amount with
   | Some amt =>
       withdrawReward amt
   | None =>
       withdrawReward zero
   end
end

transition confirmStakeTransferSuccessCallback()
    isRecompoundingContract _sender;
    empty_map = Emp ByStr20 Uint128;
    backers_deposit := empty_map;
    amount_deposited := zero
end

procedure transferStakeOwnershipToOwner()
    IsOwner _sender;
    msg = {
    _tag: "RequestDelegatorSwap";
    _recipient: staking_contract_proxy;
    _amount: zero;
    new_deleg_addr: _sender
  };
  msgs = one_msg msg;
  send msgs
end

transition ownerWithdraw(gzil_amount: Uint128)
    IsOwner _sender;
    transferStakeOwnershipToOwner;
    ownerBalanceWithdraw_pd;
    transferGzilToOwner gzil_amount
end
